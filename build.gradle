plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.2.61'
    id 'org.jetbrains.intellij' version '0.4.0'
    id "de.undercouch.download" version "3.4.3"
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile "org.jetbrains.kotlin:kotlin-reflect"
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

group 'org.sk'
version "$version.$buildNumber"

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
    main {
        java.srcDir 'src'
        kotlin.srcDir 'src'
        resources.srcDir 'res'
    }
    test {
        java.srcDir 'testSrc'
        java.srcDir 'testsData'
    }
}
kotlin {
    experimental {
        coroutines "enable"
    }
}

task testsJar(type: Jar, dependsOn: classes) {
    classifier = 'tests'
    from sourceSets.test.output
    exclude 'testData/*'
}

prepareSandbox {
    from(testsJar) {
        into "training/lib"
    }
}

prepareTestingSandbox {
    from(testsJar) {
        into "training/lib"
    }
}

runIde {
    systemProperties System.properties.findAll {(it.key as String).startsWith("idea") || (it.key as String).startsWith("jb")}
    /* Need to split the space-delimited value in the exec.args */
    print systemProperties
    args System.getProperty("exec.args", "").split(",")
}

test{
    def sysProps = System.properties.findAll { (it.key as String).startsWith("idea") || (it.key as String).startsWith("jb") }
    sysProps.put("idea.gui.tests.gradle.runner", true) //Use Gradle Launcher to run GUI tests
    systemProperties sysProps
}

intellij {
    version 'IU-183.5153.4'
    updateSinceUntilBuild false
    //to check compatibility for Python and Go
    plugins = [
            'org.jetbrains.plugins.ruby:2018.3.20181220',
            'com.intellij.testGuiFramework:0.9.37@nightly'
    ]
    if (System.getProperty("idea.gui.test.alternativeIdePath") != null) {
        alternativeIdePath System.getProperty("idea.gui.test.alternativeIdePath")
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
